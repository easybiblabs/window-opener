#include <avr/wdt.h>
#if ARDUINO > 18
#include <SPI.h>
#endif
#include <Ethernet.h>
#include <TextFinder.h>
#include <Time.h>

#if 1 /* DEBUG */
#define DEBUG(x) Serial.print(x)
#define DEBUGLN(x) Serial.println(x)
#else
#define DEBUG(x) do {} while (0)
#define DEBUGLN(x) do {} while (0)
#endif

//Digital port of the "open relay"
#define WINDOW_OPEN_RELAY  2
//Digital port of the "close relay"
#define WINDOW_CLOSE_RELAY 3
//Maximum time in seconds the open button can be triggered
#define MAX_OPEN_TIME      47
//IP address
byte ip[]  = { 192, 168, 178, 178 };
byte mac[] = { 0x5A, 0xA2, 0xDA, 0x0D, 0x12, 0x34 }; 

#define IDLE 0
#define OPENING 1
#define CLOSING 2
int STATE;
int OPEN_STOP_TIME;
int OPEN_TIME_ACCU;

EthernetServer server(80);

//setup routine
void setup() {
    Serial.begin(9600);
    DEBUGLN("Hello.");

    //enable watchdog
    wdt_enable(WDTO_2S);

    //configuring the two relays
    DEBUGLN("Init Relay");
    pinMode(WINDOW_OPEN_RELAY, OUTPUT);
    pinMode(WINDOW_CLOSE_RELAY, OUTPUT);
    digitalWrite(WINDOW_OPEN_RELAY, HIGH);
    digitalWrite(WINDOW_CLOSE_RELAY, HIGH);

    //start up the ethernet server
    DEBUG("Init IP");
    //dhcp:
    //Ethernet.begin(mac);
    //use this for static ip:
    Ethernet.begin(mac, ip);
    DEBUG(".");

    server.begin();
    DEBUGLN(".");
    DEBUGLN("Init done");
    DEBUG(freeRam());
    DEBUGLN(" Bytes Free.");
}

void loop() {
    wdt_reset();
    handle_request();
    wdt_reset();
    handle_window();
}

void update_state(int state, int time)
{
    DEBUG("updating state to ");
    DEBUGLN(state);
    DEBUGLN(time);
    STATE = state;
    OPEN_STOP_TIME = now() + time;
    if (STATE == CLOSING) {
        OPEN_TIME_ACCU = 0;
    } else {
        OPEN_TIME_ACCU += time;
    }
    DEBUGLN("updating done");
}

int freeRam ()
{
    extern int __heap_start, *__brkval;
    int v;
    return (int) &v - (__brkval == 0 ? (int) &__heap_start : (int) __brkval);
}

/**
 * Handle the HTTP request
 */
void handle_request()
{
    EthernetClient client = server.available();

    if (!client) {
        return;
    }

    TextFinder finder(client);

    if (finder.find("GET")) {
        DEBUGLN("Got a GET request");
        while (finder.findUntil("window", "\n\r")) {
            DEBUGLN("Found window request");
            handle_window_request(client, finder);
            goto end_request;
        }
    }
    DEBUGLN("I have no idea what to do.");
    print_index_page(client);

end_request:
    client.flush();
    client.stop();
}

/**
 * Apply the current state to the window controls.
 */
void handle_window()
{
    if (STATE == OPENING) {
        DEBUGLN("OPENING");
        DEBUGLN(OPEN_STOP_TIME);
        DEBUGLN(now());
        if (OPEN_STOP_TIME > now()) {
            enable(WINDOW_OPEN_RELAY);
        } else {
            disable(WINDOW_OPEN_RELAY);
            update_state(IDLE, 0);
        }
    } else if (STATE == CLOSING) {
        DEBUGLN("CLOSING");
        disable(WINDOW_OPEN_RELAY);
        enable(WINDOW_CLOSE_RELAY);
        delay(100);
        disable(WINDOW_CLOSE_RELAY);
        update_state(IDLE, 0);
    }
}

/**
 * Handle the window request; update the state.
 *
 * @param EthernetClient &client the connection
 * @param TextFinder &finder the request text finder
 *
 * this is far from perfect: it just fetches the next int value 
 * from the request strings and acts on it. Since we already forwarded
 * the "pointer" in the main routine to the string window, it works for us.
 * if the integer value is 0, it triggers the close button,
 * if it is >0 and <MAX_OPEN_TIME, it triggers the open button,
 * and errors otherwise
 */
void handle_window_request(EthernetClient &client, TextFinder  &finder) 
{
    print_header(client);
    int amount = finder.getValue();
    if (amount == 0) {
        client.println("{'action':'close'}");
        update_state(CLOSING, 0);
    } else if ((amount > 0) && (amount <= MAX_OPEN_TIME)) {
        client.print("{'action':'open','time':");
        client.print(amount);
        client.println("}");
        update_state(OPENING, amount);
    } else if (amount < 0) {
        // status
        client.print("{'state': ");
        client.print(STATE);
        client.print(", 'remaining_time': ");
        if (STATE == OPENING) {
            client.print(OPEN_STOP_TIME - now());
        } else {
            client.print(0);
        }
        client.print(", 'openness': ");
        client.print(OPEN_TIME_ACCU);
        client.print("}");
    } else {
        client.println("{'action':'error','message':'Unknown value'}");
        update_state(IDLE, 0);
    }
    DEBUGLN("window request done");
}

/**
 * Enable the relais
 * @param int pin - the digital pin to trigger
 */
void enable(int pin)
{
    DEBUG("ENABLING pin ");
    DEBUGLN(pin);
    digitalWrite(pin, LOW);
}

/**
 * Disable the relais
 * @param int pin - the digital pin to trigger
 */
void disable(int pin)
{
    DEBUG("DISABLING pin ");
    DEBUGLN(pin);
    digitalWrite(pin, HIGH);
}

/**
 * Prints HTTP header
 * @param EthernetClient &client the connection
 */
void print_header(EthernetClient &client)
{
    client.println("HTTP/1.1 200 OK");
    client.println("Content-Type: text/html; charset=UTF-8");
    client.println();
}


/**
 * Print minified version of click.html
 * @param EthernetClient &client the connection
 */
void print_index_page(EthernetClient &client)
{
    /**
     * This is the gzipped click.html (It's gzipped to keep the
     * heap and stack from colliding.)
     * Watch the number of bytes free between stack and heap
     * printed on the serial console on device init.
     * To regen this, do `make` and copy click.html.gz.h here
     * (#include doesn't work for some reason)
     */
    unsigned char click_html_gz[] = {
      0x1f, 0x8b, 0x08, 0x08, 0xbf, 0xca, 0xbf, 0x53, 0x00, 0x03, 0x63, 0x6c,
      0x69, 0x63, 0x6b, 0x2e, 0x68, 0x74, 0x6d, 0x6c, 0x00, 0xb5, 0x56, 0xdf,
      0x6f, 0xd3, 0x30, 0x10, 0x7e, 0xe7, 0xaf, 0xf0, 0xc4, 0x26, 0xa7, 0x40,
      0xed, 0x74, 0xdd, 0x34, 0xa9, 0x4d, 0xcb, 0x03, 0xf0, 0x00, 0x12, 0x0c,
      0x89, 0x49, 0x88, 0x47, 0xd7, 0xb9, 0x35, 0xde, 0x52, 0x3b, 0xd8, 0xee,
      0x4a, 0x85, 0xfa, 0xbf, 0x73, 0x4e, 0xd2, 0xfc, 0xe8, 0x56, 0xc4, 0x1e,
      0x90, 0x5a, 0xd5, 0xce, 0xdd, 0x7d, 0xf7, 0xdd, 0xf9, 0x3b, 0xa7, 0xc9,
      0xc9, 0xfb, 0xeb, 0x77, 0x37, 0x3f, 0xbe, 0x7e, 0x20, 0x99, 0x5f, 0xe5,
      0xf3, 0x17, 0x49, 0xf8, 0x21, 0xb9, 0xd0, 0xcb, 0x19, 0x05, 0x4d, 0xe7,
      0x2f, 0x08, 0x49, 0x32, 0x10, 0x69, 0x58, 0xe0, 0xd2, 0x2b, 0x9f, 0xc3,
      0xfc, 0xbb, 0xd2, 0xa9, 0xd9, 0x9c, 0x24, 0xbc, 0xda, 0x56, 0xa6, 0x5c,
      0xe9, 0x7b, 0x62, 0x21, 0x9f, 0x51, 0xe7, 0xb7, 0x39, 0xb8, 0x0c, 0xc0,
      0x53, 0x92, 0x59, 0xb8, 0x9d, 0x51, 0xce, 0x35, 0xf8, 0x54, 0x0b, 0xb6,
      0x30, 0xc6, 0x3b, 0x6f, 0x45, 0x21, 0x53, 0xcd, 0xa4, 0x59, 0xf1, 0xe6,
      0x01, 0x1f, 0xb3, 0x11, 0x1b, 0x71, 0xe9, 0x5c, 0xfb, 0x8c, 0xad, 0x14,
      0x7a, 0x39, 0x47, 0xff, 0x5f, 0x8a, 0xa1, 0xcf, 0x60, 0x05, 0xbd, 0x44,
      0x09, 0xdf, 0x17, 0x9c, 0x2c, 0x4c, 0xba, 0x25, 0xd6, 0xe4, 0x30, 0xa3,
      0xa9, 0x91, 0xeb, 0x15, 0x68, 0xbf, 0xe7, 0x92, 0xaa, 0x07, 0x22, 0x73,
      0xe1, 0xdc, 0x8c, 0x4a, 0xa3, 0xbd, 0x50, 0x1a, 0x2c, 0x29, 0xc1, 0x86,
      0x2e, 0x33, 0x1b, 0x29, 0x1c, 0xd0, 0x3a, 0x74, 0x85, 0xc6, 0x3a, 0xac,
      0x1f, 0x58, 0x88, 0x25, 0x0c, 0x43, 0x32, 0xb0, 0x8d, 0x3d, 0xf4, 0x7b,
      0x34, 0xbf, 0xc9, 0x94, 0x23, 0xf8, 0xd9, 0x94, 0x9d, 0x66, 0x48, 0x69,
      0xd4, 0x00, 0x70, 0x44, 0x68, 0x36, 0x45, 0x1b, 0x87, 0x3b, 0x51, 0x37,
      0xe3, 0x25, 0x25, 0xa9, 0xf0, 0x62, 0x58, 0xb7, 0xe6, 0x6d, 0x05, 0x33,
      0xbb, 0xb8, 0xa2, 0xfb, 0xd4, 0x0b, 0xaf, 0x09, 0x7e, 0x87, 0xf9, 0xb2,
      0xfc, 0x29, 0xac, 0x5a, 0x09, 0xbb, 0xa5, 0xf3, 0xeb, 0x02, 0x34, 0x19,
      0xc5, 0xf1, 0x59, 0xc2, 0xc5, 0x73, 0xa0, 0xc7, 0x97, 0xc7, 0xa0, 0xdd,
      0x5a, 0x4a, 0x08, 0xad, 0x2d, 0xa1, 0xaf, 0x2e, 0x9f, 0x8b, 0x7c, 0x3e,
      0x3e, 0x86, 0xac, 0xf4, 0xad, 0xa9, 0x61, 0x2f, 0x9f, 0x4d, 0xf8, 0x28,
      0xdf, 0x8d, 0xb0, 0x5a, 0xe9, 0x65, 0xd3, 0x8a, 0xe7, 0x02, 0xc7, 0xc7,
      0x80, 0x53, 0x1c, 0xaa, 0x70, 0xd0, 0xef, 0x72, 0xe3, 0xa0, 0x03, 0x9a,
      0xf0, 0xe2, 0x49, 0x71, 0x58, 0xb3, 0xb4, 0xa1, 0x71, 0xa4, 0x14, 0x3b,
      0x2a, 0x50, 0xb9, 0x22, 0x17, 0xdb, 0x89, 0x36, 0x1a, 0xba, 0x72, 0x09,
      0x31, 0x2a, 0xed, 0x06, 0x1c, 0x20, 0x0c, 0x17, 0xc2, 0x92, 0xee, 0xa6,
      0x39, 0x93, 0x5a, 0xa0, 0x7b, 0x1b, 0x9a, 0x9a, 0x6c, 0x1b, 0x95, 0xfa,
      0x6c, 0x42, 0xe2, 0xb3, 0x29, 0xed, 0x56, 0x2f, 0x03, 0xf9, 0xb4, 0x4d,
      0xde, 0x13, 0x63, 0x6f, 0x93, 0x8d, 0x4b, 0x56, 0xce, 0x0b, 0xbf, 0xc6,
      0xd3, 0x47, 0x09, 0x8f, 0xeb, 0xd1, 0x69, 0xdd, 0x12, 0x27, 0xad, 0x2a,
      0x3c, 0x71, 0x56, 0xce, 0x68, 0xe6, 0x7d, 0xe1, 0x26, 0x9c, 0x8b, 0x3b,
      0xf1, 0x8b, 0x2d, 0x8d, 0x59, 0xe6, 0x20, 0x0a, 0xe5, 0xca, 0x19, 0x0e,
      0xcf, 0x78, 0xae, 0x16, 0x8e, 0xdf, 0xfd, 0x5c, 0x83, 0xdd, 0x72, 0x1c,
      0xe4, 0x11, 0x8b, 0xeb, 0x5d, 0x39, 0xbe, 0x77, 0x65, 0x92, 0x0a, 0xf0,
      0x09, 0xf4, 0x7f, 0xbf, 0x1f, 0xee, 0x0e, 0x6f, 0xa0, 0xa3, 0xc8, 0xfb,
      0x5a, 0x4f, 0x23, 0xca, 0xf0, 0x84, 0xe9, 0x80, 0x19, 0x1d, 0x51, 0x99,
      0x2b, 0x79, 0x4f, 0xbc, 0x59, 0xcb, 0x0c, 0x8b, 0xb7, 0x9e, 0xbe, 0x21,
      0xb7, 0x6b, 0x2d, 0xbd, 0x42, 0x23, 0x0c, 0xc8, 0xef, 0xa6, 0x79, 0xa7,
      0x6c, 0x09, 0x3e, 0x3a, 0x8d, 0x80, 0xa1, 0x1b, 0x2e, 0x07, 0x4c, 0x78,
      0x6f, 0x23, 0xda, 0xa8, 0x8a, 0x0e, 0x06, 0xd3, 0xda, 0x7b, 0xd7, 0xac,
      0x1e, 0xf0, 0x30, 0xd7, 0x05, 0xfa, 0x00, 0x99, 0xb5, 0xc0, 0x8f, 0x70,
      0x3f, 0x7d, 0xbb, 0xfe, 0x12, 0xb5, 0xaa, 0x1c, 0x8e, 0x90, 0x5d, 0x8a,
      0xca, 0x89, 0x9a, 0x90, 0x90, 0xa6, 0x1b, 0x56, 0x41, 0x17, 0x60, 0x25,
      0x5e, 0x73, 0x78, 0x33, 0x21, 0xfc, 0x67, 0xe1, 0x33, 0x66, 0xcd, 0x5a,
      0xa7, 0x11, 0xde, 0x09, 0xaf, 0xca, 0x10, 0x66, 0x70, 0x2e, 0x34, 0xaa,
      0x85, 0x70, 0x72, 0x71, 0xd5, 0x32, 0xac, 0x1b, 0xf1, 0xb2, 0x91, 0xe1,
      0xbe, 0x9c, 0x52, 0x50, 0xd8, 0x84, 0xbd, 0xa4, 0x28, 0x79, 0xdd, 0xcd,
      0xf2, 0x9a, 0xd0, 0x33, 0xda, 0x43, 0x09, 0x34, 0x3c, 0xfc, 0xf2, 0x48,
      0x80, 0x56, 0x7a, 0xa3, 0x5d, 0xb3, 0xba, 0x25, 0x51, 0x27, 0x7e, 0x4e,
      0xe2, 0x7e, 0x15, 0xa4, 0x09, 0x0e, 0x4c, 0x7b, 0xa1, 0xbb, 0xce, 0xda,
      0x6d, 0x94, 0x97, 0x19, 0xa9, 0x6a, 0xaa, 0x54, 0x7a, 0x88, 0x13, 0x2e,
      0x71, 0x32, 0x9a, 0xf4, 0xf0, 0xc2, 0xd5, 0x30, 0x25, 0x0b, 0x0b, 0xe2,
      0x7e, 0xfa, 0xd8, 0xf9, 0x7c, 0xd2, 0x63, 0x7e, 0xc4, 0x79, 0x77, 0x50,
      0x4d, 0x19, 0x72, 0xd2, 0x56, 0x7b, 0xc8, 0xe3, 0xa0, 0xad, 0x85, 0xb0,
      0x58, 0x7b, 0x34, 0x60, 0xe1, 0x45, 0x13, 0x0d, 0xa6, 0x7f, 0xf3, 0x0d,
      0xd0, 0x25, 0xfe, 0x13, 0x6e, 0xf5, 0x68, 0x0e, 0x58, 0xa6, 0x52, 0xe8,
      0xe3, 0xec, 0x08, 0xe4, 0x58, 0xce, 0xbf, 0xd1, 0x78, 0x1c, 0x7e, 0x80,
      0xdf, 0x92, 0x78, 0x82, 0x72, 0xdb, 0x8c, 0x56, 0xe3, 0xbb, 0xfd, 0xa2,
      0x12, 0x7a, 0x1b, 0xe0, 0xc0, 0x7f, 0xd4, 0x1e, 0xec, 0x83, 0xc8, 0xa3,
      0xca, 0xf6, 0x86, 0x9c, 0xc7, 0x71, 0x5c, 0x3b, 0x74, 0xa7, 0x34, 0xe1,
      0xe1, 0xed, 0x8d, 0xff, 0x68, 0x78, 0xf5, 0xcf, 0xe6, 0x0f, 0xf4, 0x38,
      0x59, 0x14, 0xea, 0x08, 0x00, 0x00
    };
    unsigned int click_html_gz_len = 834;

    client.println("HTTP/1.1 200 OK");
    client.println("Content-Type: text/html; charset=UTF-8");
    client.println("Content-Encoding: gzip");
    client.println();
    client.write(click_html_gz, click_html_gz_len);
}

